/***************************************************
 *                 probabilidad.bc                 *
 ***************************************************

 Autor: Marc Meléndez Schofield

 Definiciones de funciones matemáticas:

 - media aritmética y desviación estándar
 - factorial
 - número combinatorio
 - distribuciones binomial, normal y de Poisson.

 Se permite la reproducción y modificación total o 
 parcial.

/*  Media aritmética (los datos se guardan en la matriz x[],
    y en n se indica el número de datos) */

define media(x[], n) {

  suma = 0;

  for(i = 1; i <= n; i++)
    suma += x[i];
  return suma/n;
}

/*  Desviación estándar  */

define desv(x[], n) {

  nvar = 0;
  med = media(x[], n);

  for(i = 1; i <= n; i++)
    nvar += (x[i] - med)^2;

  return sqrt(nvar/n);
}

/* Definición recursiva de la función factorial */

define factorial(x) {
  if (x <= 1) return (1);
  return (factorial(x-1) * x);
}

/* Números combinatorios */

define comb(n, m) {
  return factorial(n)/(factorial(m)*factorial(n - m));
}

/* Distribución binomial */

define binom(r, n, p) {
  return comb(n,r)* p^r * (1 - p)^(n - r);
}

/* Pi se define como 4 * arctg(1) */

pi = 4*a(1);

/* Distribución normal */

define norm(x, mu, sigma) {
  return (1/(sigma*sqrt(2*pi)))*e(-((x - mu)^2)/(2*sigma^2));
}

/* Distribución de Poisson */

define poisson(k, lambda) {
  return lambda^k * e(-lambda)/factorial(k);
}
